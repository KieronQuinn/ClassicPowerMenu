plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs'
    id 'com.google.protobuf'
    id 'kotlin-parcelize'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.google.devtools.ksp'
}

def version = '1.9'

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.kieronquinn.app.classicpowermenu"
        minSdk 30
        targetSdk 34
        versionCode 190
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "TAG_NAME", "\"${version}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        aidl true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.kieronquinn.app.classicpowermenu'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.2"
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    api "androidx.security:security-crypto:1.1.0-alpha06"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'


    //Room for data storage
    api "androidx.room:room-runtime:$room_version"
    api "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    //AndroidX
    def lifecycle_version = "2.8.4"
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation "androidx.activity:activity-ktx:1.9.1"
    implementation "androidx.fragment:fragment-ktx:1.8.2"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.work:work-runtime-ktx:2.9.1'
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //Material
    implementation 'com.google.android.material:material:1.12.0'

    //Monet
    implementation 'com.github.KieronQuinn:MonetCompat:0.4.1'

    //Dependency injection
    implementation "io.insert-koin:koin-android:$koin_version"

    //Protobuf
    implementation "com.google.protobuf:protobuf-javalite:3.25.2"

    //Guava for hashing code (to match what Gpay uses)
    implementation 'com.google.guava:guava:31.1-android'

    //Stubs for hidden APIs. Shouldn't be included in APK.
    compileOnly project(':systemstubs')

    //packages/apps/QuickAccessWallet import
    implementation project(':QuickAccessWallet')

    //Device Controls code imported from SystemUI
    implementation project(':Controls')

    //Flexbox for buttons
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    //zxing for barcode/QR preview
    implementation 'com.google.zxing:core:3.4.1'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0@aar'

    //libsu for root operations
    def libsuVersion = '5.2.2'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"

    //Shizuku for helpful system service code (wallet/controls don't work with just Shizuku)
    def shizuku_version = '13.1.5'
    implementation "dev.rikka.shizuku:api:$shizuku_version"

    //Xposed
    compileOnly 'de.robv.android.xposed:api:82'

    //OSS libraries activity
    implementation 'com.google.android.gms:play-services-oss-licenses:17.1.0'

    //Retrofit for update checking
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //Other third party
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
    implementation 'com.airbnb.android:lottie:6.3.0'
    implementation 'com.jakewharton:process-phoenix:2.1.2'
    implementation 'me.saket:better-link-movement-method:2.2.0'
    implementation "io.noties.markwon:core:4.6.2"

    //Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}